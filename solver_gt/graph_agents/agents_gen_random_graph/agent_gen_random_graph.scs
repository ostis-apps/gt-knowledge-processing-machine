agent_gen_random_graph
=> nrel_main_idtf:
	[агентная scp-программа  генерации случайного графа] (* <- lang_ru;; *);
	[agent scp-program of generating a random graph] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_gen_random_graph = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

    _-> rrel_1:: .._agent_gen_random_graph_operator_start (*
        _<- searchElStr3;;
        _-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
        _-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
        _-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

        _=> nrel_goto:: .._agent_gen_random_graph_operator1;;
    *);;

    _-> .._agent_gen_random_graph_operator1 (*
        _<- searchElStr3;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_generate_random_graph;;
        _-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
        _-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

        _=> nrel_then:: .._agent_gen_random_graph_operator_message;;
        _=> nrel_else:: .._agent_gen_random_graph_operator_return;;
    *);;

	_-> .._agent_gen_random_graph_operator_return (*
        _<- return;;

    *);;

    _-> .._agent_gen_random_graph_operator_message (*
        _<- printNl;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_const:: [Start work agent_gen_random_graph];;

        _=> nrel_goto:: .._agent_gen_random_graph_operator_get_arg1;;
    *);;

    _-> .._agent_gen_random_graph_operator_get_arg1 (*
        _<- searchElStr5;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
        _-> rrel_2:: rrel_assign:: rrel_scp_var:: rrel_const_pos_perm:: _arc1;;
        _-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_link:: _param1;;
        _-> rrel_4:: rrel_assign:: rrel_scp_var:: rrel_const_pos_perm:: _arc2;;
        _-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_1;;

        _=> nrel_then:: .._agent_gen_random_graph_operator_get_arg2;;
        _=> nrel_else:: .._agent_gen_random_graph_operator_unsuccess;;
    *);;

    _-> .._agent_gen_random_graph_operator_unsuccess (*
        _<- genElStr3;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_finished_unsuccessfully;;
        _-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
        _-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

        _=> nrel_goto:: .._agent_gen_random_graph_operator_return;;
    *);;

    _-> .._agent_gen_random_graph_operator_get_arg2 (*
        _<- searchElStr5;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
        _-> rrel_2:: rrel_assign:: rrel_scp_var:: rrel_const_pos_perm:: _arc1;;
        _-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_link:: _param2;;
        _-> rrel_4:: rrel_assign:: rrel_scp_var:: rrel_const_pos_perm:: _arc2;;
        _-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_2;;

        _=> nrel_then:: .._agent_gen_random_graph_operator_check1_par2;;
        _=> nrel_else:: .._agent_gen_random_graph_operator_unsuccess;;
    *);;

    _-> .._agent_gen_random_graph_operator_check1_par2 (*
        _<- ifGr;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param2;;
        _-> rrel_2:: rrel_fixed:: rrel_scp_const:: [-1];;

        _=> nrel_then:: .._agent_gen_random_graph_operator_check2_par2;;
        _=> nrel_else:: .._agent_gen_random_graph_operator_print_error1;;
    *);;
    
    _-> .._agent_gen_random_graph_operator_print_error1 (*
        _<- printNl;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_const:: [Error: second argument value must be greater than or equal to 0];;

        _=> nrel_goto:: .._agent_gen_random_graph_operator_unsuccess;;
    *);;

    _-> .._agent_gen_random_graph_operator_check2_par2 (*
        _<- ifGr;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param2;;
        _-> rrel_2:: rrel_fixed:: rrel_scp_const:: [100];;

        _=> nrel_then:: .._agent_gen_random_graph_operator_print_error2;;
        _=> nrel_else:: .._agent_gen_random_graph_call_operator;;
    *);;

    _-> .._agent_gen_random_graph_operator_print_error2 (*
        _<- printNl;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_const:: [Error: second argument value must be 100 or lower];;

        _=> nrel_goto:: .._agent_gen_random_graph_operator_unsuccess;;
    *);;

    _-> .._agent_gen_random_graph_call_operator (*
        _<- call;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_gen_random_graph;;
        _-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_gen_random_graph_params (*
                _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param1;;
                _-> rrel_2:: rrel_fixed:: rrel_scp_var:: _param2;;
                _-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_struct:: _result;;
        *);;
        _-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr;;

        _=> nrel_goto:: .._agent_gen_random_graph_wait_operator;;
    *);;

    _-> .._agent_gen_random_graph_wait_operator (*
        _<- waitReturn;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr;;

        _=> nrel_goto:: .._agent_check_result_value;;
    *);;

    _-> .._agent_check_result_value (*
        _<- ifVarAssign;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _result;;

        _=> nrel_then:: .._agent_gen_random_graph_operator_take_result;;
        _=> nrel_else:: .._agent_gen_random_graph_operator_unsuccess;;
    *);;

    _-> .._agent_gen_random_graph_operator_take_result (*
        _<- genElStr5;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
        _-> rrel_2:: rrel_assign:: rrel_scp_var:: rrel_common:: rrel_const:: _arc_to_result;;
        _-> rrel_3:: rrel_fixed:: rrel_scp_var:: _result;;
        _-> rrel_4:: rrel_assign:: rrel_scp_var:: rrel_pos_const_perm:: _arc_to_arc;;
        _-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_result;;

        _=> nrel_goto:: .._agent_gen_random_graph_operator_success;;
    *);;

     _-> .._agent_gen_random_graph_operator_success (*
        _<- genElStr3;;
        _-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_finished_successfully;;
        _-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
        _-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

        _=> nrel_goto:: .._agent_gen_random_graph_operator_return;;
    *);;

*);;

*];;