scp_program -> proc_of_finding_graph_minimum_spanning_tree
(*
    -> rrel_params: ..proc_of_finding_graph_minimum_spanning_tree_params
    (*
        -> rrel_1: rrel_in: _graph;;
        -> rrel_2: rrel_out: _spanning_tree;;
    *);;

    -> rrel_operators: ..proc_of_finding_graph_minimum_spanning_tree_operators
    (*
        -> rrel_init: ..proc_of_finding_graph_minimum_spanning_tree_operator1
        (*
            <- genEl;;

            -> rrel_1: rrel_assign: rrel_scp_var: rrel_node: rrel_const: _spanning_tree;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator2_0;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator2_0
        (*
            <- searchElStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _edge;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
            -> rrel_5: rrel_fixed: rrel_scp_const: rrel_oredge;;
            
            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator2;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator_of_successfull_solving;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator2
        (*
            <- searchSetStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _vertex;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
            -> rrel_5: rrel_fixed: rrel_scp_const: rrel_vertex;;

	    -> rrel_set_3: rrel_assign: rrel_scp_var: _graph_vertices_set;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator3;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator_of_successfull_solving;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator3
        (*
            <- genElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _spanning_tree;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _vertex;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator4;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator4
        (*
            <- searchSetStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph_vertices_set;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _vertex;;

            -> rrel_set_3: rrel_assign: rrel_scp_var: _copy_graph_vertices_set;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator5;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator_of_successfull_solving;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator5
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _copy_graph_vertices_set;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _vertex;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator6;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator_of_successfull_solving;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator6
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc2;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator7;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator7
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _minimum_weigth;;
            -> rrel_2: rrel_fixed: rrel_scp_const: rrel_link: [9999];;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator8;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator8
        (*
            <- searchSetStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph_vertices_set;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _vertex;;

            -> rrel_set_3: rrel_assign: rrel_scp_var: _second_copy_graph_vertices_set;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator9;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator_graph_struct_error;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator9
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _second_copy_graph_vertices_set;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _first_inc_vertex;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator10;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator29;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator10
        (*
            <- printNl;;

            -> rrel_1: rrel_fixed: rrel_scp_const: [second];;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator11;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator11
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc2;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator12;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator12
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _spanning_tree;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _first_inc_vertex;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator13;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator9;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator13
        (*
            <- searchSetStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph_vertices_set;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _vertex;;

            -> rrel_set_3: rrel_assign: rrel_scp_var: _third_copy_graph_vertices_set;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator14;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator_graph_struct_error;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator14
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _third_copy_graph_vertices_set;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _second_inc_vertex;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator15;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator9;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator15
        (*
            <- printNl;;

            -> rrel_1: rrel_fixed: rrel_scp_const: [third];;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator16;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator16
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc2;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator17;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator17
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _spanning_tree;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _second_inc_vertex;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator14;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator18;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator18
        (*
            <- printNl;;

            -> rrel_1: rrel_fixed: rrel_scp_const: [finding for edge];;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator19;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator19
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _first_inc_vertex;;
            -> rrel_2: rrel_assign: rrel_scp_var: _edge;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _second_inc_vertex;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator20_1;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator24;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator20_1
        (*
            <- searchSetStr3;;

            -> rrel_1: rrel_assign: rrel_scp_var: _edge_weigth_node;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _edge;;

            -> rrel_set_1: rrel_assign: rrel_scp_var: _set_with_weigth;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator20_2;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator24;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator20_2
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _set_with_weigth;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _edge_weigth_node;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator20_3;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator24;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator20_3
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc2;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator20_4;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator20_4
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_const: concept_weight;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _edge_weigth_node;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator20_5;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator20_2;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator20_5
        (*
            <- searchElStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _edge_weigth_node;;
            -> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _edge_weigth;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc3;;
            -> rrel_5: rrel_fixed: rrel_scp_const: nrel_system_identifier;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator21;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator24;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator21
        (*
            <- ifGr;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _minimum_weigth;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _edge_weigth;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator22;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator24;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator22
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _minimum_weigth;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _edge_weigth;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator23_1;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator23_1
        (*
            <- printNl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _minimum_weigth;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator23_2;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator23_2
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _chosed_edge;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _edge;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator23_3;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator23_3
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _chosed_second_inc_vertex;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _second_inc_vertex;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator24;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator24
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _second_inc_vertex;;
            -> rrel_2: rrel_assign: rrel_scp_var: _edge;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _first_inc_vertex;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator25_1;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator14;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator25_1
        (*
            <- searchSetStr3;;

            -> rrel_1: rrel_assign: rrel_scp_var: _edge_weigth_node;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _edge;;

            -> rrel_set_1: rrel_assign: rrel_scp_var: _set_with_weigth;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator25_2;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator14;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator25_2
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _set_with_weigth;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _edge_weigth_node;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator25_3;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator14;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator25_3
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc2;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator25_4;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator25_4
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_const: concept_weight;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _edge_weigth_node;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator25_5;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator25_2;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator25_5
        (*
            <- searchElStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _edge_weigth_node;;
            -> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _edge_weigth;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc3;;
            -> rrel_5: rrel_fixed: rrel_scp_const: nrel_system_identifier;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator26;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator14;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator26
        (*
            <- ifGr;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _minimum_weigth;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _edge_weigth;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator27;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator14;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator27
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _minimum_weigth;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _edge_weigth;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator28_1;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator28_1
        (*
            <- printNl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _minimum_weigth;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator28_2;;
        *);;

	-> ..proc_of_finding_graph_minimum_spanning_tree_operator28_2
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _chosed_edge;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _edge;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator28_3;;
        *);;

	-> ..proc_of_finding_graph_minimum_spanning_tree_operator28_3
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _chosed_second_inc_vertex;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _second_inc_vertex;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator14;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator29
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _spanning_tree;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _chosed_edge;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator31;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator30;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator30
        (*
            <- genElStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _spanning_tree;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _chosed_edge;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
            -> rrel_5: rrel_fixed: rrel_scp_const: rrel_edge;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator31;;
        *);;
        
           -> ..proc_of_finding_graph_minimum_spanning_tree_operator31
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _spanning_tree;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _chosed_second_inc_vertex;;

            => nrel_then: ..proc_of_finding_graph_minimum_spanning_tree_operator33;;
            => nrel_else: ..proc_of_finding_graph_minimum_spanning_tree_operator32;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator32
        (*
            <- genElStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _spanning_tree;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _chosed_second_inc_vertex;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
            -> rrel_5: rrel_fixed: rrel_scp_const: rrel_vertex;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator33;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator33
        (*
            <- printNl;;
            
            -> rrel_1: rrel_fixed: rrel_scp_const: [Vertex and edge have been added.];;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator5;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator_of_successfull_solving
        (*
            <- printNl;;
            
            -> rrel_1: rrel_fixed: rrel_scp_const: [Minimum spanning tree for graph has been found.];;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator_of_nrel_arc_generation;;
        *);;
        
        -> ..proc_of_finding_graph_minimum_spanning_tree_operator_of_nrel_arc_generation
        (*
            <- genElStr5;;
            
            -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            -> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _spanning_tree;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
            -> rrel_5: rrel_fixed: rrel_scp_const: nrel_minimum_spanning_tree;;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator_return;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator_graph_struct_error
        (*
            <- printNl;;
            
            -> rrel_1: rrel_fixed: rrel_scp_const: [Incorrect graph representation!];;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator_return;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator_error
        (*
            <- printNl;;
            
            -> rrel_1: rrel_fixed: rrel_scp_const: [Error was occured...];;

            => nrel_goto: ..proc_of_finding_graph_minimum_spanning_tree_operator_return;;
        *);;

        -> ..proc_of_finding_graph_minimum_spanning_tree_operator_return
        (*
            <- return;;
        *);;
    *);;
*);;
