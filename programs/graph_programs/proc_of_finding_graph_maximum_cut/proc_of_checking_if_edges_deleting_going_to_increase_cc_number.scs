scp_program -> proc_of_checking_if_edges_deleting_going_to_increase_cc_number
(*
    -> rrel_params: ...
    (*
        -> rrel_1: rrel_in: _graph;;
        -> rrel_2: rrel_in: _deleting_edges;;
        -> rrel_3: rrel_in: _connected_components_number;;
        -> rrel_4: rrel_out: _is_going;;
    *);;

    -> rrel_operators: ...
    (*
        -> rrel_init: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator0
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _is_going;;
            -> rrel_2: rrel_fixed: rrel_scp_const: [0];;

            => nrel_goto: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator1;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator1
        (*
            <- searchSetStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _deleting_edges;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _del_edge;;

            -> rrel_set_3: rrel_assign: rrel_scp_var: _copy_deleting_edges;;

            => nrel_then: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator2;;
            => nrel_else: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator_return;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator2
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _copy_deleting_edges;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _del_edge;;

            => nrel_then: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator3;;
            => nrel_else: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator6;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator3
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc2;;

            => nrel_goto: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator4;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator4
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _del_edge;;

            => nrel_then: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator5;;
            => nrel_else: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator2;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator5
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc2;;

            => nrel_goto: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator2;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator6
        (*
            <- call;;

            -> rrel_1: rrel_fixed: rrel_scp_const: proc_of_finding_connected_components_number;;
            -> rrel_2: rrel_fixed: rrel_scp_const: ...
            (*
                -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
                -> rrel_2: rrel_assign: rrel_scp_var: _del_connected_components_number;;
            *);;
            -> rrel_3: rrel_assign: rrel_scp_var: _descriptor;;

            => nrel_goto: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator7;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator7
        (*
            <- waitReturn;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _descriptor;;

            => nrel_goto: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator8;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator8
        (*
            <- searchSetStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _deleting_edges;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _del_edge;;

            -> rrel_set_3: rrel_assign: rrel_scp_var: _copy_deleting_edges;;

            => nrel_then: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator9;;
            => nrel_else: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator_return;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator9
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _copy_deleting_edges;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _del_edge;;

            => nrel_then: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator10;;
            => nrel_else: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator12;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator10
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc2;;

            => nrel_goto: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator11;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator11
        (*
            <- genElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _del_edge;;

            => nrel_goto: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator9;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator12
        (*
            <- contAdd;;

            -> rrel_1: rrel_assign: rrel_scp_var: _temp;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _connected_components_number;;
            -> rrel_3: rrel_fixed: rrel_scp_const: [1];;

            => nrel_goto: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator13;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator13
        (*
            <- ifEq;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _temp;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _del_connected_components_number;;

            => nrel_then: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator14;;
            => nrel_else: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator_return;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator14
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _is_going;;
            -> rrel_2: rrel_fixed: rrel_scp_const: [1];;

            => nrel_goto: ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator_return;;
        *);;

        -> ..proc_of_checking_if_edges_deleting_going_to_increase_cc_number_operator_return
        (*
            <- return;;
        *);;
    *);;
*);;
