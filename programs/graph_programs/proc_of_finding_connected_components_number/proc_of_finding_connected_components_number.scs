scp_program -> proc_of_finding_connected_components_number
(*
    -> rrel_params: ...
    (*
        -> rrel_1: rrel_in: _graph;;
        -> rrel_2: rrel_out: _connected_components_number;;
    *);;

    -> rrel_operators: ...
    (*
        -> rrel_init: ..proc_of_finding_connected_components_number_operator0_1
        (*
            <- genEl;;

            -> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _checked_vertices;;

            => nrel_goto: ..proc_of_finding_connected_components_number_operator0;;
        *);;
        
        -> ..proc_of_finding_connected_components_number_operator0
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _connected_components_number;;
            -> rrel_2: rrel_fixed: rrel_scp_const: [0];;

            => nrel_goto: ..proc_of_finding_connected_components_number_operator1;;
        *);;
        
        -> ..proc_of_finding_connected_components_number_operator1
        (*
            <- searchSetStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _some_vertex;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
            -> rrel_5: rrel_fixed: rrel_scp_const: rrel_vertex;;

            -> rrel_set_3: rrel_assign: rrel_scp_var: _graph_vertices;;

            => nrel_then: ..proc_of_finding_connected_components_number_operator2;;
            => nrel_else: ..proc_of_finding_connected_components_number_operator_return;;
        *);;

        -> ..proc_of_finding_connected_components_number_operator2
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph_vertices;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _some_vertex;;

            => nrel_then: ..proc_of_finding_connected_components_number_operator3;;
            => nrel_else: ..proc_of_finding_connected_components_number_operator_return;;
        *);;

        -> ..proc_of_finding_connected_components_number_operator3
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc2;;

            => nrel_goto: ..proc_of_finding_connected_components_number_operator4;;
        *);;

        -> ..proc_of_finding_connected_components_number_operator4
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _checked_vertices;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _some_vertex;;

            => nrel_then: ..proc_of_finding_connected_components_number_operator2;;
            => nrel_else: ..proc_of_finding_connected_components_number_operator5_0;;
        *);;
        
        -> ..proc_of_finding_connected_components_number_operator5_0
        (*
            <- printEl;;
            
            -> rrel_1: rrel_fixed: rrel_scp_var: _checked_vertices;;
            
            => nrel_goto: ..proc_of_finding_connected_components_number_operator5;;
        *);;  

        -> ..proc_of_finding_connected_components_number_operator5
        (*
            <- genEl;;

            -> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _current_subgraph;;

            => nrel_goto: ..proc_of_finding_connected_components_number_operator6;;
        *);;

        -> ..proc_of_finding_connected_components_number_operator6
        (*
            <- call;;

            -> rrel_1: rrel_fixed: rrel_scp_const: proc_of_finding_connected_component;;
            -> rrel_2: rrel_fixed: rrel_scp_const: ...
            (*
                -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
                -> rrel_2: rrel_fixed: rrel_scp_var: _some_vertex;;
                -> rrel_3: rrel_fixed: rrel_scp_var: _checked_vertices;;
                -> rrel_4: rrel_fixed: rrel_scp_var: _current_subgraph;;
            *);;
            -> rrel_3: rrel_assign: rrel_scp_var: _descriptor;;

            => nrel_goto: ..proc_of_finding_connected_components_number_operator7;;
        *);;

        -> ..proc_of_finding_connected_components_number_operator7
        (*
            <- waitReturn;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _descriptor;;

            => nrel_goto: ..proc_of_finding_connected_components_number_operator8;;
        *);;

        -> ..proc_of_finding_connected_components_number_operator8
        (*
             <- contAdd;;

             -> rrel_1: rrel_assign: rrel_scp_var: _temp;;
             -> rrel_2: rrel_fixed: rrel_scp_var: _connected_components_number;;
             -> rrel_3: rrel_fixed: rrel_scp_const: [1];;

             => nrel_goto: ..proc_of_finding_connected_components_number_operator9;;
        *);;

        -> ..proc_of_finding_connected_components_number_operator9
        (*
            <- varAssign;;

            -> rrel_1: rrel_assign: rrel_scp_var: _connected_components_number;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _temp;;

            => nrel_goto: ..proc_of_finding_connected_components_number_operator10;;
        *);;

        -> ..proc_of_finding_connected_components_number_operator10
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _current_subgraph;;

            => nrel_goto: ..proc_of_finding_connected_components_number_operator2;;
        *);;

        -> ..proc_of_finding_connected_components_number_operator_return
        (*
            <- genElStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            -> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _connected_components_number;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
            -> rrel_5: rrel_fixed: rrel_scp_const: nrel_connected_components_number;;
            
            => nrel_goto: ..proc_of_finding_connected_components_number_operator_return_end;;
        *);;
        
        -> ..proc_of_finding_connected_components_number_operator_return_end
        (*
            <- return;;
        *);;
    *);;
*);;
