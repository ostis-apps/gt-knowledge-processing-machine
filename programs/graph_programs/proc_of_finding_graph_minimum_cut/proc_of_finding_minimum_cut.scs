scp_program -> proc_of_finding_minimum_cut
(*
    -> rrel_params: ...
    (*
        -> rrel_1: rrel_in: _graph;;
        -> rrel_2: rrel_out: _minimum_cut;;
    *);;

    -> rrel_operators: ...
    (*
        -> rrel_init: ..proc_of_finding_minimum_cut_operator1
        (*
            <- genEl;;

            -> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _minimum_cut;;

            => nrel_goto: ..proc_of_finding_minimum_cut_operator2;;
        *);;

        -> ..proc_of_finding_minimum_cut_operator2
        (*
            <- searchSetStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _some_edge;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
            -> rrel_5: rrel_fixed: rrel_scp_const: rrel_oredge;;

            -> rrel_set_3: rrel_assign: rrel_scp_var: _graph_edges;;

            => nrel_then: ..proc_of_finding_minimum_cut_operator3;;
            => nrel_else: ..proc_of_finding_minimum_cut_operator_return;;  
        *);;

        -> ..proc_of_finding_minimum_cut_operator3
        (*
            <- call;;

            -> rrel_1: rrel_fixed: rrel_scp_const: proc_of_finding_connected_components_number;;
            -> rrel_2: rrel_fixed: rrel_scp_const: ...
            (*
                -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
                -> rrel_2: rrel_assign: rrel_scp_var: _connected_components_number;;
            *);;
            -> rrel_3: rrel_assign: rrel_scp_var: _descriptor;;

            => nrel_goto: ..proc_of_finding_minimum_cut_operator4;;
        *);;

        -> ..proc_of_finding_minimum_cut_operator4
        (*
            <- waitReturn;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _descriptor;;

            => nrel_goto: ..proc_of_finding_minimum_cut_operator5;;
        *);;

        -> ..proc_of_finding_minimum_cut_operator5
        (*
            <- searchElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph_edges;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_assign: rrel_scp_var: _some_edge;;

            => nrel_then: ..proc_of_finding_minimum_cut_operator6;;
            => nrel_else: ..proc_of_finding_minimum_cut_operator_success;;  
        *);;

        -> ..proc_of_finding_minimum_cut_operator6
        (*
            <- eraseEl;;

            -> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc2;;

            => nrel_goto: ..proc_of_finding_minimum_cut_operator7;;
        *);;

        -> ..proc_of_finding_minimum_cut_operator7
        (*
            <- genEl;;

            -> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _deleting_edges;;

            => nrel_goto: ..proc_of_finding_minimum_cut_operator8;;
        *);;

        -> ..proc_of_finding_minimum_cut_operator8
        (*
            <- genElStr3;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _deleting_edges;;
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _some_edge;;

            => nrel_goto: ..proc_of_finding_minimum_cut_operator9;;
        *);;

        -> ..proc_of_finding_minimum_cut_operator9
        (*
            <- call;;

            -> rrel_1: rrel_fixed: rrel_scp_const: proc_of_chosing_cut;;
            -> rrel_2: rrel_fixed: rrel_scp_const: ...
            (*
                -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
                -> rrel_2: rrel_fixed: rrel_scp_var: _connected_components_number;;
                -> rrel_3: rrel_fixed: rrel_scp_var: _deleting_edges;;
                -> rrel_4: rrel_fixed: rrel_scp_var: _minimum_cut;;
            *);;
            -> rrel_3: rrel_assign: rrel_scp_var: _descriptor;;

            => nrel_goto: ..proc_of_finding_minimum_cut_operator10;;
        *);;

        -> ..proc_of_finding_minimum_cut_operator10
        (*
            <- waitReturn;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _descriptor;;

            => nrel_goto: ..proc_of_finding_minimum_cut_operator5;;
        *);;

        -> ..proc_of_finding_minimum_cut_operator_success
        (*
            <- genElStr5;;

            -> rrel_1: rrel_fixed: rrel_scp_var: _graph;;
            -> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _arc2;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _minimum_cut;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
            -> rrel_5: rrel_fixed: rrel_scp_const: nrel_minimum_cut;;

            => nrel_goto: ..proc_of_finding_minimum_cut_operator_return;;
        *);;

        -> ..proc_of_finding_minimum_cut_operator_return
        (*
            <- return;;
        *);;
    *);;
*);;
