scp_program -> proc_find_leaf
    (*
    -> rrel_params: .proc_find_leaf_params (*
        -> rrel_1: rrel_in: _parameter;;
        -> rrel_2: rrel_in: _answer;;
    *);;
 
    -> rrel_operators: .proc_find_leaf_operator_set
        (*
 
        -> rrel_init: .proc_find_leaf_operator1
            (*
                <- contAssign;;
                -> rrel_1: rrel_assign: rrel_scp_var: _number_of_v;;
                -> rrel_2: rrel_fixed: rrel_scp_const: [0];;

                => nrel_goto: .proc_find_leaf_operator2;;
            *);;
 
        -> .proc_find_leaf_operator2
            (*
                <- searchSetStr5;;
                -> rrel_1: rrel_fixed: rrel_scp_var: _parameter;;
                -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
                -> rrel_3: rrel_assign: rrel_scp_var: _el3;;
                -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
                -> rrel_5: rrel_fixed: rrel_scp_const: rrel_vertex;;
           
                -> rrel_set_3: rrel_assign: rrel_scp_var: _vertexes;;
 
                => nrel_then: .proc_find_leaf_operator3;;      
                => nrel_else: ..proc_add_graph_in_answer0;;
            *);;
 
        -> .proc_find_leaf_operator3
            (*
                <- searchElStr3;;
                -> rrel_1:rrel_fixed: rrel_scp_var: _vertexes;;
                -> rrel_2:rrel_assign: rrel_scp_var: _current_arc;;
                -> rrel_3:rrel_assign: rrel_scp_var: _current_vertex;;
 
                => nrel_then: .proc_find_leaf_operator3_1;;
                => nrel_else: ..proc_add_graph_in_answer0;;
            *);;

        -> .proc_find_leaf_operator3_1
            (*
                <- genEl;;
                -> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _unchecked;;
 
                => nrel_goto: .proc_find_leaf_operator4;;
            *);;
 
        -> .proc_find_leaf_operator4
            (*
                <- eraseEl;;
                -> rrel_1:rrel_fixed:rrel_erase:rrel_scp_var:_current_arc;;
 
                => nrel_goto: .proc_find_leaf_operator5;;
            *);;
 
        -> .proc_find_leaf_operator5
            (*
                <- searchSetStr5;;
                -> rrel_1: rrel_fixed: rrel_scp_var: _current_vertex;;
                -> rrel_2: rrel_assign: rrel_scp_var: _arc2;;
                -> rrel_3: rrel_assign: rrel_scp_var: _el3;;
                -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
                -> rrel_5: rrel_fixed: rrel_scp_var: _parameter;;
           
                -> rrel_set_3: rrel_fixed: rrel_scp_var: _unchecked;;
 
                => nrel_goto: .proc_find_leaf_operator6;;
            *);;
 
        -> .proc_find_leaf_operator6
            (*
                <- searchSetStr5;;
                -> rrel_1: rrel_assign: rrel_scp_var: _el1;;
                -> rrel_2: rrel_assign: rrel_scp_var: _arc2;;
                -> rrel_3: rrel_fixed: rrel_scp_var: _current_vertex;;
                -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
                -> rrel_5: rrel_fixed: rrel_scp_var: _parameter;;
           
                -> rrel_set_1: rrel_fixed: rrel_scp_var: _unchecked;;
 
                => nrel_goto: .proc_find_leaf_operator7;;
            *);;
 
        -> .proc_find_leaf_operator7
            (*
                <- searchElStr3;;
                -> rrel_1:rrel_fixed: rrel_scp_var: _unchecked;;
                -> rrel_2:rrel_assign: rrel_scp_var: _tmp_arc;;
                -> rrel_3:rrel_assign: rrel_scp_var: _tmp_vertex;;
 
                => nrel_then: .proc_find_leaf_operator8;;
                => nrel_else: .proc_find_leaf_operator3;;
            *);;

        -> .proc_find_leaf_operator8
            (*
                <- eraseEl;;
                -> rrel_1:rrel_fixed:rrel_erase:rrel_scp_var:_tmp_arc;;
 
                => nrel_goto: .proc_find_leaf_operator9;;
            *);;
 
        -> .proc_find_leaf_operator9
            (*
                <- searchElStr3;;
                -> rrel_1:rrel_fixed: rrel_scp_var: _unchecked;;
                -> rrel_2:rrel_assign: rrel_scp_var: _tmp_arc;;
                -> rrel_3:rrel_assign: rrel_scp_var: _tmp_vertex;;
 
                => nrel_then: .proc_find_leaf_operator3;;
                => nrel_else: .proc_find_leaf_operator10;;
            *);;

        -> .proc_find_leaf_operator10 
            (*
                <- contAdd;;
                -> rrel_1: rrel_fixed: rrel_scp_var: _number_of_v;;
                -> rrel_2: rrel_fixed: rrel_scp_var: _number_of_v;;
                -> rrel_3: rrel_fixed: rrel_scp_const: [1];;

                => nrel_goto: .proc_find_leaf_operator3;;
            *);;
 
        -> ..proc_add_graph_in_answer0 
        (*
            <- genElStr5;;
            -> rrel_1:rrel_fixed: rrel_scp_var: _parameter;;
            -> rrel_2: rrel_assign: rrel_common: rrel_const : rrel_scp_var: _arc22;;
            -> rrel_3:rrel_fixed: rrel_scp_var: _number_of_v;;
            -> rrel_4:rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc42;;
            -> rrel_5:rrel_fixed: rrel_scp_const: nrel_number_of_leaf_vertexes;;
 
            => nrel_goto: ..proc_add_graph_in_answer01;;
        *);;
 
        -> ..proc_add_graph_in_answer01 
        (*
            <- genElStr3;;
            -> rrel_1:rrel_fixed: rrel_scp_var: _answer;;
            -> rrel_2:rrel_assign: rrel_scp_var: _arc1;;
            -> rrel_3:rrel_fixed: rrel_scp_const: nrel_number_of_leaf_vertexes;;
 
            => nrel_goto: ..proc_add_graph_in_answer02;;
        *);;
 
        -> ..proc_add_graph_in_answer02 
        (*
            <- genElStr3;;
            -> rrel_1:rrel_fixed: rrel_scp_var: _answer;;
            -> rrel_2:rrel_assign: rrel_scp_var: _arc1;;
            -> rrel_3:rrel_fixed: rrel_scp_var: _parameter;;
 
            => nrel_goto: ..proc_add_graph_in_answer03;;
        *);;
 
        -> ..proc_add_graph_in_answer03 (*
            <- genElStr3;;
            -> rrel_1:rrel_fixed: rrel_scp_var: _answer;;
            -> rrel_2:rrel_assign: rrel_scp_var: _arc1;;
            -> rrel_3:rrel_fixed: rrel_scp_var: _arc42;;
 
            => nrel_goto: ..proc_add_graph_in_answer04;;
        *);;

        -> ..proc_add_graph_in_answer04 (*
            <- genElStr3;;
            -> rrel_1:rrel_fixed: rrel_scp_var: _answer;;
            -> rrel_2:rrel_assign: rrel_scp_var: _arc1;;
            -> rrel_3:rrel_fixed: rrel_scp_var: _arc22;;
 
            => nrel_goto: ..proc_add_graph_in_answer05;;
        *);;

        -> ..proc_add_graph_in_answer05 (*
            <- genElStr3;;
            -> rrel_1:rrel_fixed: rrel_scp_var: _answer;;
            -> rrel_2:rrel_assign: rrel_scp_var: _arc1;;
            -> rrel_3:rrel_fixed: rrel_scp_var: _number_of_v;;
 
            => nrel_goto: ..proc_add_graph_in_answer1;;
        *);;
 
        -> ..proc_add_graph_in_answer1 (*
            <- call;;
            -> rrel_1: rrel_fixed: rrel_scp_const: proc_add_graph_in_ans;;
            -> rrel_2: rrel_fixed: rrel_scp_const: ... (*
                -> rrel_1: rrel_fixed: rrel_scp_var: _parameter;;
                -> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
            *);;
            -> rrel_3: rrel_assign: rrel_scp_var: _descr;;
 
            => nrel_goto: ..proc_add_graph_in_answer2;;
        *);;
 
        -> ..proc_add_graph_in_answer2 (*
            <- waitReturn;;
            -> rrel_1: rrel_fixed: rrel_scp_var: _descr;;
 
            => nrel_goto: .proc_find_leaf_operator_return;;
        *);;
 
        -> .proc_find_leaf_operator_return
            (*
                <- return;;
            *);;
        *);;
    *);;